if (HDEEM_LIBRARIES AND HDEEM_INCLUDE_DIRS)
    set (HDEEM_FIND_QUIETLY TRUE)
endif()

# Yes, CMAKE is not clever enough to search in LIBRARY_PATH etc!
find_path(HDEEM_INCLUDE_DIRS NAMES hdeem.h HINTS ${HDEEM_INCLUDE_DIRS} ${HDEEM_ROOT_DIR}/include ENV CPATH C_INCLUDE_PATH)
find_library(HDEEM_LIBRARIES NAMES hdeem HINTS ${HDEEM_LIBRARIES} ${HDEEM_ROOT_DIR}/lib ENV LIBRARY_PATH LD_LIBRARY_PATH)

include(FindPackageHandleStandardArgs)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(HDEEM DEFAULT_MSG HDEEM_LIBRARIES HDEEM_INCLUDE_DIRS)

if(HDEEM_FOUND)
    add_library(hdeem::hdeem UNKNOWN IMPORTED GLOBAL)
    set_target_properties(hdeem::hdeem PROPERTIES
        IMPORTED_LINK_INTERFACE_LANGUAGES "C"
        IMPORTED_LOCATION "${HDEEM_LIBRARIES}"
        INTERFACE_INCLUDE_DIRECTORIES "${HDEEM_INCLUDE_DIRS}"
    )
    target_link_libraries(hdeem::hdeem INTERFACE rt FreeIPMI::FreeIPMI)

    list(APPEND HDEEM_LIBRARIES "rt" "freeipmi")
    mark_as_advanced(HDEEM_INCLUDE_DIRS HDEEM_LIBRARIES)
endif()
